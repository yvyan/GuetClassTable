apply plugin: 'com.android.application'

def appStoreFile = System.getenv("STORE_FILE")
def appStorePassword = System.getenv("STORE_PASSWORD")
def appKeyAlias = System.getenv("KEY_ALIAS")
def appKeyPassword = System.getenv("KEY_PASSWORD")
def properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
if (appStoreFile == null) {
    appStoreFile = properties.getProperty('appStoreFile');
}
if (appStorePassword == null) {
    appStorePassword = properties.getProperty('appStorePassword');
}
if (appKeyAlias == null) {
    appKeyAlias = properties.getProperty('appKeyAlias');
}
if (appKeyPassword == null) {
    appKeyPassword = properties.getProperty('appKeyPassword');
}

android {

    defaultConfig {
        applicationId "top.yvyan.guettable"
        minSdkVersion 21
        compileSdk 34
        targetSdkVersion 34
        versionCode 67 //记得补丁版本号归零(patch_version)
        versionName "5.4.5"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        android.buildFeatures.dataBinding = true

        ndk {
            //noinspection ChromeOsAbiSupport
            abiFilters 'arm64-v8a'
        }
    }

    signingConfigs {
        if(appStoreFile) {
            sign {
                storeFile file(appStoreFile)
                storePassword appStorePassword
                keyAlias = appKeyAlias
                keyPassword appKeyPassword
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (appStoreFile) {
                signingConfig signingConfigs.sign
            }
        }
        debug {
            if (appStoreFile) {
                signingConfig signingConfigs.sign
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    namespace 'top.yvyan.guettable'
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.github.zfman:TimetableView:v2.0.7'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-savedstate:2.7.0'

    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.google.code.gson:gson:2.10.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    //XUI
    implementation 'com.github.xuexiangjys:XUI:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    //友盟移动统计
    implementation  'com.umeng.umsdk:common:9.6.7'
    implementation  'com.umeng.umsdk:asms:1.8.2'
    //友盟APM
    implementation 'com.umeng.umsdk:apm:1.9.5'
    //友盟在线参数
    implementation  'com.umeng.umsdk:abtest:1.0.1' //使用ABTest模块必选
    //图片裁剪
    implementation 'com.github.yalantis:ucrop:2.2.8-native'
    //xUpdate
    implementation 'com.github.xuexiangjys:XUpdate:2.1.4'
    implementation 'com.zhy:okhttputils:2.6.2'
    implementation 'com.github.xuexiangjys.XUtil:xutil-core:2.0.0'
    //键值对存储
    implementation 'com.tencent:mmkv:1.3.3'
    //富文本
    implementation 'com.zzhoujay.richtext:richtext:3.0.8'
    //工具类
    implementation 'com.github.xuexiangjys.XUtil:xutil-core:2.0.0'
    implementation 'com.github.xuexiangjys.XUtil:xutil-sub:2.0.0'

    implementation 'com.zhy:okhttputils:2.6.2'
}